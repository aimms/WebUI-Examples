## ams_version=1.0

Model Main_DataChangeMonitorTest {
    StringParameter IdentifierToMonitor;
    Set monitoredIdentifiers {
        SubsetOf: AllIdentifiers;
    }
    ElementParameter dataChangeMonitor {
        Range: webui::RegisteredDataChangeMonitors;
    }
    Parameter onDataChangeProcedureWasCalled {
        Default: 0;
    }
    Procedure SetupMonitor {
        Body: {
            if(dataChangeMonitor) then
            	webui::DataChangeMonitorUnregisterCallback(dataChangeMonitor);
            endif;
            
            monitoredIdentifiers := {'IdentifierToMonitor'};
            webui::DataChangeMonitorRegisterCallback(monitoredIdentifiers,'OnDataChange',dataChangeMonitor);
        }
    }
    Procedure OnDataChange {
        Body: {
            onDataChangeProcedureWasCalled := 1;
        }
    }
    Procedure RunTests {
        Body: {
            Display "Start tests...";
            numTests := 0;
            numFailed := 0;
            
            IdentifierToMonitor := 0;
            
            !--------------------------------------------
            Display "Test 1: Initially it should not be called";
            numTests += 1;
            onDataChangeProcedureWasCalled := 0;
            SetupMonitor();
            
            webui::DataChangeMonitorsUpdate();
            
            if(onDataChangeProcedureWasCalled) then
            	Display "FAILED -- Not expected datachange, but was detected";
            	numFailed += 1;
            else
            	Display "SUCCESS";
            endif;
            onDataChangeProcedureWasCalled := 0;
            !--------------------------------------------
            
            !--------------------------------------------
            Display "Test 2: After a data change it should be called";
            numTests += 1;
            onDataChangeProcedureWasCalled := 0;
            IdentifierToMonitor += 1;
            
            webui::DataChangeMonitorsUpdate();
            
            if(not onDataChangeProcedureWasCalled) then
            	Display "FAILED -- Expected datachange, but was not detected";
            	numFailed += 1;
            else
            	Display "SUCCESS";
            endif;
            onDataChangeProcedureWasCalled := 0;
            !--------------------------------------------
            
            !--------------------------------------------
            Display "Test 3: After a data change and a reset it should not be called";
            numTests += 1;
            onDataChangeProcedureWasCalled := 0;
            IdentifierToMonitor += 1;
            
            webui::DataChangeMonitorReset2(dataChangeMonitor);
            
            webui::DataChangeMonitorsUpdate();
            
            if(onDataChangeProcedureWasCalled) then
            	Display "FAILED -- Not expected datachange, but was detected";
            	numFailed += 1;
            else
            	Display "SUCCESS";
            endif;
            onDataChangeProcedureWasCalled := 0;
            !--------------------------------------------
            
            message := FormatString("End of tests, %n tests, %n failed\n\n", numTests, numFailed);
            Put message;
        }
        Parameter numFailed;
        Parameter numTests;
        StringParameter message;
    }
    Procedure MainInitialization;
    Procedure MainExecution;
    Procedure MainTermination {
        Body: {
            return DataManagementExit();
        }
    }
}
